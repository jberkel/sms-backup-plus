apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.zegoggles.smssync"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1577
        versionName "1.5.12"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig keystoreProperties.isEmpty() ? null : signingConfigs.release
        }
    }

    lintOptions {
        // http://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.LintOptions.html#com.android.build.gradle.internal.dsl.LintOptions
        warningsAsErrors true
    }

    //  avoid "Duplicate files copied in APK META-INF/"
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.github.jberkel.k-9:k9mail-library:eaf689025e'
    implementation 'com.android.billingclient:billing:2.0.3'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.6'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation "com.google.truth:truth:0.39"
    testImplementation 'org.mockito:mockito-all:1.10.17'
}

tasks.withType(JavaCompile) {
    if (!it.name.toLowerCase().contains('test')) {
        options.compilerArgs = ['-Werror', '-Xlint:unchecked', '-Xlint:deprecation']
    }
}
