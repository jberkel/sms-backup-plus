apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.zegoggles.smssync"
        minSdkVersion 9
        targetSdkVersion 10
        versionCode 1557
        versionName "1.5.11-beta4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig keystoreProperties.isEmpty() ? null : signingConfigs.release
        }
    }

    useLibrary 'org.apache.http.legacy'
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    testOptions {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

}

dependencies {
    compile('oauth.signpost:signpost-commonshttp4:1.2.1.2') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    compile 'com.google.android:support-v4:r7'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.github.jberkel:k-9:75bb293984'
    compile 'com.android.billingclient:billing:dp-1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.5'
    testCompile 'org.robolectric:shadows-httpclient:3.0'
    testCompile 'com.squareup:fest-android:1.0.5'
    testCompile 'org.mockito:mockito-all:1.10.17'
}

task checkVersion {
    def versionName = android.defaultConfig.versionName
    doFirst {
        //  # make sure new version is propagated everywhere
        if (!(new File('CHANGES').text.contains(versionName))) {
            throw new RuntimeException("CHANGES not updated")
        }
        if (!(new File('app/src/main/assets/about.html').text.contains("SMS Backup+ $versionName"))) {
            throw new RuntimeException("about.html not updated")
        }
    }

}

task(tag(type: Exec) {
    executable "sh"
    args "-c", """
        git commit --allow-empty -a -m 'Release $android.defaultConfig.versionName' &&
        git tag $android.defaultConfig.versionName &&
        git push --tags origin master
    """
}).dependsOn(checkVersion)
